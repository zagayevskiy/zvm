@include<lisp/automem.zc>
@include<lisp/rbtree.zc>

# LispEnv struct
# (Dict . OuterLispEnv)
# retainedSubGraphsStackEntry is a key-value pair with linked-list based stack value contains all child envs
struct LispRuntimeContext {
    var mem: AutoMemory;
    var globalEnv: Cons;
    var atomT: Cons;
    var retainedSubGraphsStackEntry: Cons;
}

fn createRuntimeContext(mem: AutoMemory): LispRuntimeContext {
    val context: LispRuntimeContext = alloc(sizeof<LispRuntimeContext>);

    context.mem = mem;
    context.globalEnv = createEnv(mem, nil);
    context.atomT = makeAtom(mem, "T");

    val globalDict = envDict(context.globalEnv);
    context.retainedSubGraphsStackEntry = putRbTree(mem, globalDict, makeAtom(mem, "retained env refs stack"), nil, ::compare);

    fillGlobalWithStandardFunctions(context);

    return context;
}

fn freeRuntimeContext(context: LispRuntimeContext) {
    free(context);
}

fn createEnv(mem: AutoMemory, outerEnv: Cons): Cons {
    val dict: Cons = cast<[void]>(makeRbTree(mem));
    return cons(mem, dict, outerEnv);
}

fn envDict(env: Cons): RbTree {
    return cast<[void]>(car(env));
}

fn envOuterEnv(env: Cons): Cons {
    return cdr(env);
}

fn envLookup(env: Cons, key: Cons): Cons {
    var found: Cons = nil;
    var currentEnv = env;
    while (found == nil && currentEnv != nil) {
        found = findTree(envDict(currentEnv).root, key, ::compare);
        currentEnv = envOuterEnv(currentEnv);
    }

    return found;
}

fn fillGlobalWithStandardFunctions(context: LispRuntimeContext) {
    val dict = envDict(context.globalEnv);
    val mem = context.mem;
    val atomT = context.atomT;

    dictPut(mem, dict, atomT, atomT);

    putNamedLambda(mem, dict, "+", ::lispPlus, true);
    putNamedLambda(mem, dict, "-", ::lispMinus, true);
    putNamedLambda(mem, dict, "*", ::lispMul, true);

    # we want compare functions to be lazy so them need to eval args themselves
    putNamedLambda(mem, dict, "=", ::lispEq, false);
    putNamedLambda(mem, dict, "<", ::lispStrictlyAscOrder, false);
    putNamedLambda(mem, dict, ">", ::lispStrictlyDescOrder, false);
    putNamedLambda(mem, dict, "<=", ::lispAscOrder, false);
    putNamedLambda(mem, dict, ">=", ::lispDescOrder, false);


    # we also want logical 'or' and 'and' to be lazy
    putNamedLambda(mem, dict, "||", ::lispOr, false);
    putNamedLambda(mem, dict, "&&", ::lispAnd, false);

    putNamedLambda(mem, dict, "nil?", ::lispIsNil, true);
    putNamedLambda(mem, dict, "number?", ::lispIsNumber, true);
    putNamedTcoLambda(mem, dict, "cond", ::lispTcoCond, false);

    putNamedLambda(mem, dict, "quote", ::lispQuote, false);
    putNamedLambda(mem, dict, "def!", ::lispDef, false);
    putNamedLambda(mem, dict, "panic!", ::lispPanic, true);
    putNamedLambda(mem, dict, "fn*", ::lispLambda, false);
    putNamedTcoLambda(mem, dict, "let*", ::lispTcoLet, false);

    putNamedLambda(mem, dict, "list", ::lispList, true);
    putNamedLambda(mem, dict, "cons", ::lispCons, true);

    putNamedLambda(mem, dict, "car", ::lispCar, true);
    putNamedLambda(mem, dict, "cdr", ::lispCdr, true);
    putNamedLambda(mem, dict, "caar", ::lispCaar, true);
    putNamedLambda(mem, dict, "cadr", ::lispCadr, true);
    putNamedLambda(mem, dict, "cdar", ::lispCdar, true);
    putNamedLambda(mem, dict, "cddr", ::lispCddr, true);
    putNamedLambda(mem, dict, "caaar", ::lispCaaar, true);
    putNamedLambda(mem, dict, "caadr", ::lispCaadr, true);
    putNamedLambda(mem, dict, "cadar", ::lispCadar, true);
    putNamedLambda(mem, dict, "cdaar", ::lispCdaar, true);
    putNamedLambda(mem, dict, "caddr", ::lispCaddr, true);
    putNamedLambda(mem, dict, "cddar", ::lispCddar, true);
    putNamedLambda(mem, dict, "cdadr", ::lispCdadr, true);
    putNamedLambda(mem, dict, "cdddr", ::lispCdddr, true);
}

fn eval(context: LispRuntimeContext, env: Cons, enterSexpr: Cons): Cons {

#    printAutoMemoryInfo(context.mem);
    var currentEnv = env;
    var sexpr = enterSexpr;

    retainSubGraph(context, currentEnv);
#    print("eval:");
#    printCons(enterSexpr);
#    endline();
#    println("runtime:");
#    printCons(envLookup(context.globalEnv, makeAtom(context.mem, "plus")));
#    endline();
    gc(context.mem, context.globalEnv);

    while(true) {
#        println("before gc:");
#        printAutoMemoryInfo(context.mem);
#        println("after gc:");
#        printAutoMemoryInfo(context.mem);
#        print("eval step:");
#        printCons(sexpr);
#        print("\n");
        val sexprType = getType(sexpr);
        if (sexprType == CT_NIL || sexprType == CT_INT) {
            releaseSubGraph(context, currentEnv);
            return sexpr;
        }
        if (sexprType == CT_ATOM) {
            releaseSubGraph(context, currentEnv);
            return envLookup(currentEnv, sexpr);
        }

        if (sexprType == CT_LIST) {
            val f = eval(context, currentEnv, car(sexpr));
            val args = cdr(sexpr);
            val regularFnRef = tryGetRegularFnRef(f);
            if (regularFnRef != nil) {
                val result = callRegularLambda(context, regularFnRef, isEvalArgs(f), currentEnv, args);
                if (isTcoLambda(f)) {
                    sexpr = car(result);
#                    print("tco call expr:");
#                    printCons(sexpr);
#                    endline();
                    val newEnv = cdr(result);
                    if (newEnv != currentEnv) {
                        releaseSubGraph(context, currentEnv);
                        retainSubGraph(context, newEnv);
                        currentEnv = newEnv;
                    }
                } else {
                    releaseSubGraph(context, currentEnv);
                    return result;
                }
            } else if (isUserDefinedLambda(f)) {
                val bodyAndEnv = evalUserDefinedLambda(context, currentEnv, f, args);
                sexpr = car(bodyAndEnv);
                val newEnv = cdr(bodyAndEnv);
                if (newEnv != currentEnv) {
                    releaseSubGraph(context, currentEnv);
                    retainSubGraph(context, newEnv);
                    currentEnv = newEnv;
                }
            } else {
                printCons(sexpr);
                crashm(404, "Unknown function");
            }
        }
    }

    crashm(123, "unknown argument");
}

fn mapEval(context: LispRuntimeContext, currentEnv: Cons, list: Cons): Cons {
    var result: Cons = nil;
    var resultCursor = result;
    val mem = context.mem;
    for (var cursor = list; getType(cursor) != CT_NIL; cursor = cdr(cursor)) {
        val current = car(cursor);
        val nextCons = cons(mem, eval(context, currentEnv, current), nil);
        if (result == nil) {
            result = nextCons;
            resultCursor = result;
            retainSubGraph(context, result);
        } else {
            resultCursor.right = nextCons;
            resultCursor = resultCursor.right;
        }
    }
    if (result != nil) {
        releaseSubGraph(context, result);
    }

    return result;
}

fn putNamedLambda(mem: AutoMemory, dict: RbTree, name: [byte], nativeFunction: (LispRuntimeContext, Cons, Cons) -> Cons, evalArgs: bool) {
    dictPut(mem, dict, makeAtom(mem, name), makeRegularLambda(mem, nativeFunction, evalArgs, false));
}

fn putNamedTcoLambda(mem: AutoMemory, dict: RbTree, name: [byte], nativeFunction: (LispRuntimeContext, Cons, Cons) -> Cons, evalArgs: bool) {
     dictPut(mem, dict, makeAtom(mem, name), makeRegularLambda(mem, nativeFunction, evalArgs, true));
 }

const REGULAR_LAMBDA_CHECK_MASK = 1431655765; # 0b01010101010101010101010101010101

# returns regular function reference
# or nil if passed cons does not represents regular function reference
fn tryGetRegularFnRef(mayBeLambda: Cons): (LispRuntimeContext, Cons, Cons) -> Cons {
    if (getType(mayBeLambda) != CT_INT) return nil;
    val mayBeFnRef = cast<int>(mayBeLambda.left);
    val check = cast<int>(mayBeLambda.right) ^ REGULAR_LAMBDA_CHECK_MASK;
    if (mayBeFnRef != check) return nil;
    return cast<(LispRuntimeContext, Cons, Cons) -> Cons>(mayBeFnRef);
}

fn makeRegularLambda(mem: AutoMemory, nativeFunction: (LispRuntimeContext, Cons, Cons) -> Cons, evalArgs: bool, tco: bool): Cons {
    val nativeFunctionAddress = cast<int>(nativeFunction);
    val nativeFunctionAddressCheck = nativeFunctionAddress ^ REGULAR_LAMBDA_CHECK_MASK;
    val lambda = makeCons(mem, cast<[void]>(nativeFunctionAddress), cast<[void]>(nativeFunctionAddressCheck), CT_INT);
    setEvalArgs(lambda, evalArgs);
    setTcoLambda(lambda, tco);
    return lambda;
}

const USER_DEFINED_LAMBDA_CHECK_MASK = 858993459; # 0b00110011001100110011001100110011
# ( (binds body) . (outerEnv . outerEnv^USER_DEFINED_LAMBDA_CHECK_MASK) )
fn makeUserDefinedLambda(context: LispRuntimeContext, outerEnv: Cons, definition: Cons): Cons {
    val binds = car(definition);
    val body = cdar(definition);
    if (getType(cddr(definition)) != CT_NIL) {
        println("must be exact two params - binds and body");
        return nil;
    }
    for (var cursor = binds; getType(cursor) != CT_NIL; cursor = cdr(cursor)) {
        val bind = car(cursor);
        if (getType(bind) != CT_ATOM) {
            printCons(bind);
            println(" can not be used as bind param");
            return nil;
        }
    }
    val mem = context.mem;
    val check = makeNumber(mem, cast<int>(outerEnv) ^ USER_DEFINED_LAMBDA_CHECK_MASK);

    return cons(mem, definition, cons(mem, outerEnv, check));
}

fn isUserDefinedLambda(mayBeLambda: Cons): bool {
    if (getType(mayBeLambda) != CT_LIST) return false;
    val mayBeOuterEnvAndCheck = cdr(mayBeLambda);
    if (getType(mayBeOuterEnvAndCheck) != CT_LIST) return false;
    val mayBeOuterEnv = car(mayBeOuterEnvAndCheck);
    val mayBeOuterEnvCheck = cdr(mayBeOuterEnvAndCheck);
    if (getType(mayBeOuterEnvCheck) != CT_INT) return false;
    return cast<int>(mayBeOuterEnv) == (getInt(mayBeOuterEnvCheck) ^ USER_DEFINED_LAMBDA_CHECK_MASK);
}

fn evalUserDefinedLambda(context: LispRuntimeContext, env: Cons, lambda: Cons, args: Cons): Cons {
    val outerEnv = cdar(lambda);
    val definition = car(lambda);
    val binds = car(definition);
    val body = cdar(definition);
    val mem = context.mem;
    val lambdaEnv = createEnv(mem, outerEnv);
    retainSubGraph(context, lambdaEnv);
    val dict = envDict(lambdaEnv);

    var argsCursor = args;
    for (var cursor = binds; getType(cursor) != CT_NIL; cursor = cdr(cursor)) {
        val bind = car(cursor);
        var arg: Cons;
        if (getType(argsCursor) == CT_LIST) {
            arg = eval(context, env, car(argsCursor));
            argsCursor = cdr(argsCursor);
        } else {
            arg = nil;
        }
        dictPut(mem, dict, bind, arg);
    }
    releaseSubGraph(context, lambdaEnv);

    return cons(mem, body, lambdaEnv);
}

fn callRegularLambda(context: LispRuntimeContext, nativeFunction: (LispRuntimeContext, Cons, Cons) -> Cons, evalArgs: bool, env: Cons, args: Cons): Cons {
    val argsType = getType(args);
    if (argsType != CT_LIST && argsType != CT_NIL) {
        print("list or nil expected as args");
        crash(5);
    }

    var actualArgs: Cons;
    if (evalArgs) {
        actualArgs = mapEval(context, env, args);
    } else {
        actualArgs = args;
    }
    return nativeFunction(context, env, actualArgs);
}

fn dictPut(mem: AutoMemory, dict: RbTree, key: Cons, value: Cons): Cons {
    return putRbTree(mem, dict, key, value, ::compare);
}

fn setEvalArgs(lambda: Cons, evalArgs: bool) {
    setUserBit1(lambda, evalArgs);
}

fn isEvalArgs(lambda: Cons): bool {
    return userBit1(lambda);
}

fn setTcoLambda(lambda: Cons, tco: bool) {
    setUserBit2(lambda, tco);
}

fn isTcoLambda(lambda: Cons): bool {
    return userBit2(lambda);
}

fn retainSubGraph(context: LispRuntimeContext, subGraph: Cons) {
    if (subGraph == context.globalEnv) return;
    val stackEntry = context.retainedSubGraphsStackEntry;
    setEntryValue(stackEntry, cons(context.mem, subGraph, entryValue(stackEntry)));
}

fn releaseSubGraph(context: LispRuntimeContext, subGraph: Cons) {
    if (subGraph == context.globalEnv) return;
    val stackEntry = context.retainedSubGraphsStackEntry;
    setEntryValue(stackEntry, cdr(entryValue(stackEntry)));
}

fn peekEnv(context: LispRuntimeContext): Cons {
    return car(entryValue(context.retainedSubGraphsStackEntry));
}

fn lispPrint(cons: Cons) {
    val regularFnRef = tryGetRegularFnRef(cons);
    if (regularFnRef != nil) {
        print("built-in function ::");
        # TODO may be traverse global scope to recognize it name
        printi(cast<int>(regularFnRef));
        endline();
    } else if (isUserDefinedLambda(cons)) {
        println("lambda");
    } else {
        printCons(cons);
        endline();
    }
}

@include<lisp/std_fns.zc>