fn lispDef(context: LispRuntimeContext, env: Cons, args: Cons): CallResult {
    val key = car(args);
    val evalResult = eval(context, env, cdar(args));
    if (evalResult.err != nil) return err(context, appendStackTrace(context, key, "evaluating def* value", evalResult.err));
    dictPut(context.mem, envDict(env), key, evalResult.ok);
    return evalResult;
}

fn lispLambda(context: LispRuntimeContext, env: Cons, args: Cons): CallResult {
    return makeUserDefinedLambda(context, env, args);
}

fn lispThrow(context: LispRuntimeContext, env: Cons, args: Cons): CallResult {
    return err(context, appendStackTrace(context, makeAtom(context.mem, "seed"), "throw", nil));
}

fn lispTryCatch(context: LispRuntimeContext, env: Cons, args: Cons): CallResult {
    val tryBody = car(args);
    val tryResult = eval(context, env, tryBody);

    val catchClause = cdar(args);
    print("catch clause:");
    printCons(catchClause);
    endline();
    val catchWord = car(catchClause);
    if (getType(catchWord) != CT_ATOM || orderStrings("catch*", getAtomName(catchWord)) != 0) {
        print("catch* not found");
        # TODO may be throw...
        crash(10001);
    }

    val exceptionBindingAndCatchBody = cdr(catchClause);
    val exceptionBinding = car(exceptionBindingAndCatchBody);
    if (getType(exceptionBinding) != CT_ATOM) {
        printCons(exceptionBinding);
        print(" - exception binding recomended to be atom.");
    }

    if (tryResult.err != nil) {

        val catchBody = cdar(exceptionBindingAndCatchBody);
        val mem = context.mem;
        val catchEnv = createEnv(mem, env);
        retainSubGraph(context, catchEnv);
        dictPut(mem, envDict(catchEnv), exceptionBinding, cast<Cons>(tryResult.err));
        val catchResult = eval(context, env, catchBody);
        releaseSubGraph(context, catchEnv);
        return catchResult;
    }

    return tryResult;
}

#
# (let*
#       (key1 value1 key2 value2 ... keyN valueN)
#       body
# )
#
# this is TCO fn so it returns (resultBody . newEnv)
fn lispTcoLet(context: LispRuntimeContext, env: Cons, args: Cons): CallResult {
    var bindings = car(args);
    if (getType(bindings) == CT_NIL) {
        print("let* with empty bindings list does not make sens.");
    }

    val resultBody = cdar(args);
    if (resultBody == nil) {
        print("let* without body does not make sens. Nil returned.");
        return ok(context, cons(context.mem, nil, env));
    }

    val mem = context.mem;
    val letEnv = createEnv(mem, env);
    val letEnvDict = envDict(letEnv);

    while(getType(bindings) != CT_NIL) {
        val tail = cdr(bindings);
        val evalResult = eval(context, letEnv, car(tail));
        val binding = car(bindings);
        if (evalResult.err != nil) return err(context, appendStackTrace(context, binding, "evaluating let* value", evalResult.err));
        dictPut(mem, letEnvDict, binding, evalResult.ok);
        bindings = cdr(tail);
    }

    return ok(context, cons(mem, resultBody, letEnv));
}

# (cond
#       (condition1 result1)
#       (condition2 result2)
#       ...
#       (conditionN resultN)
# )
# (conditionX resultX) is equivalent to (conditionX . resultX)
#
# this is TCO fn so it returns (resultBody . env)
fn lispTcoCond(context: LispRuntimeContext, env: Cons, args: Cons): CallResult {
    for (var cursor = args; getType(cursor) != CT_NIL; cursor = cdr(cursor)) {
        val case = car(cursor);
        val condition = car(case);
        val evalResult = eval(context, env, condition);
        if (evalResult.err != nil) return evalResult;

        if (getType(evalResult.ok) != CT_NIL) {
            var tmp = cdr(case);
            if (getType(tmp) == CT_LIST) {
                tmp = car(tmp);
            }
            return ok(context, cons(context.mem, tmp, env));
        }
    }

    return ok(context, nil);
}

fn lispPanic(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    println("panic!");
    printCons(args);
    crash(0);
}

fn lispPlus(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    var current = car(args);
    assertType(current, CT_INT, "int expected as 1st arg of +");
    var result= getInt(current);
    for (var tail = cdr(args); getType(tail) != CT_NIL; tail = cdr(tail)) {
        current = car(tail);
        assertType(current, CT_INT, "ints expected as args of +");
        result = result + getInt(current);
    }

    return ok(context, makeNumber(context.mem, result));
}

# this fn result calculation based on count of args
# (- x y1 y2 ... yN) equals to x - y1 - y2 - ... - yN
# (- x) is -x
fn lispMinus(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    var current = car(args);
    assertType(current, CT_INT, "int expected as 1st arg of -");
    var result = getInt(current);
    var tail = cdr(args);
    if (getType(tail) == CT_NIL) {
        result = 0 - result;
    }
    for ( ; getType(tail) != CT_NIL; tail = cdr(tail)) {
        current = car(tail);
        assertType(current, CT_INT, "ints expected as args of -");
        result = result - getInt(current);
    }

    return ok(context, makeNumber(context.mem, result));
}

fn lispMul(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    var current = car(args);
    assertType(current, CT_INT, "int expected as 1st arg of *");
    var result= getInt(current);
    for (var tail = cdr(args); getType(tail) != CT_NIL; tail = cdr(tail)) {
        current = car(tail);
        assertType(current, CT_INT, "ints expected as args of *");
        result = result * getInt(current);
    }

    return ok(context, makeNumber(context.mem, result));
}

fn lispMod(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    var n = car(args);
    var d = cdar(args);
    return ok(context, makeNumber(context.mem, getInt(n) % getInt(d)));
}

fn lispDiv(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    var n = car(args);
    var d = cdar(args);
    return ok(context, makeNumber(context.mem, getInt(n) / getInt(d)));
}

fn lispOrdering(context: LispRuntimeContext, env: Cons, args: Cons, comparator: (Cons, Cons) -> bool): CallResult {
    var evalResult = eval(context, env, car(args));
    if (evalResult.err != nil) return evalResult;
    var prev = evalResult.ok;
    val retainedSubGraph = cons(context.mem, prev, nil);
    retainSubGraph(context, retainedSubGraph);
    for (var argsCursor = cdr(args); getType(argsCursor) != CT_NIL; argsCursor = cdr(argsCursor)) {
        evalResult = eval(context, env, car(argsCursor));
        if (evalResult.err != nil) {
            releaseSubGraph(context, retainedSubGraph);
            return evalResult;
        }
        val evaluated = evalResult.ok;
        if (!comparator(prev, evaluated)) {
            releaseSubGraph(context, retainedSubGraph);
            return ok(context, nil);
        }
        prev = evaluated;
        retainedSubGraph.left = prev;
    }

    releaseSubGraph(context, retainedSubGraph);
    return ok(context, context.atomT);
}

fn lispEq(context: LispRuntimeContext, env: Cons, args: Cons): CallResult {
    return lispOrdering(context, env, args, ::lispEqComparator);
}
fn lispEqComparator(prev: Cons, next: Cons): bool {
    return compare(prev, next) == 0;
}

# (< x1 x2 ... xN) means that x1 < x2 < ... < xN
fn lispStrictlyAscOrder(context: LispRuntimeContext, env: Cons, args: Cons): CallResult {
    return lispOrdering(context, env, args, ::lispStrictlyAscComparator);
}
fn lispStrictlyAscComparator(prev: Cons, next: Cons): bool {
    return compare(prev, next) < 0;
}

# (<= x1 x2 ... xN) means that x1 <= x2 <= ... <= xN
fn lispAscOrder(context: LispRuntimeContext, env: Cons, args: Cons): CallResult {
    return lispOrdering(context, env, args, ::lispAscComparator);
}
fn lispAscComparator(prev: Cons, next: Cons): bool {
    return compare(prev, next) <= 0;
}

# (> x1 x2 ... xN) means that x1 > x2 > ... > xN
fn lispStrictlyDescOrder(context: LispRuntimeContext, env: Cons, args: Cons): CallResult {
    return lispOrdering(context, env, args, ::lispStrictlyDescComparator);
}
fn lispStrictlyDescComparator(prev: Cons, next: Cons): bool {
    return compare(prev, next) > 0;
}

# (>= x1 x2 ... xN) means that x1 >= x2 >= ... >= xN
fn lispDescOrder(context: LispRuntimeContext, env: Cons, args: Cons): CallResult {
    return lispOrdering(context, env, args, ::lispDescComparator);
}
fn lispDescComparator(prev: Cons, next: Cons): bool {
    return compare(prev, next) >= 0;
}

# returns first non-nil operand or nil if hasn't
fn lispOr(context: LispRuntimeContext, env: Cons, args: Cons): CallResult {
    for (var cursor = args; getType(cursor) != CT_NIL; cursor = cdr(cursor)) {
        val evalResult = eval(context, env, car(cursor));
        if (evalResult.err != nil) return evalResult;
        val operand = evalResult.ok;
        if (getType(operand) != CT_NIL) {
            return evalResult;
        }
    }

    return ok(context, nil);
}

# returns T if all operands non-nil or nil otherwise
fn lispAnd(context: LispRuntimeContext, env: Cons, args: Cons): CallResult {
    for (var cursor = args; getType(cursor) != CT_NIL; cursor = cdr(cursor)) {
        val evalResult = eval(context, env, car(cursor));
        if (evalResult.err != nil) return evalResult;
        val operand = evalResult.ok;
        if (getType(operand) == CT_NIL) {
            return ok(context, nil);
        }
    }

    return ok(context, context.atomT);
}

fn lispIsNil(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    if (getType(car(args)) == CT_NIL && getType(cdr(args)) == CT_NIL) {
        return ok(context, context.atomT);
    }

    return ok(context, nil);
}

fn lispIsNumber(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    if (getType(car(args)) == CT_INT && getType(cdr(args)) == CT_NIL) {
        return ok(context, context.atomT);
    }

    return ok(context, nil);
}

fn lispQuote(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    return ok(context, car(args));
}

fn lispList(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    return ok(context, args);
}

fn lispCons(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    return ok(context, cons(context.mem, car(args), cdar(args)));
}

fn lispCar(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    return ok(context, car(car(args)));
}

fn lispCdr(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    return ok(context, cdr(car(args)));
}

fn lispCaar(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    return ok(context, caar(car(args)));
}

fn lispCadr(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    return ok(context, cadr(car(args)));
}

fn lispCdar(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    return ok(context, cdar(car(args)));
}

fn lispCddr(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    return ok(context, cddr(car(args)));
}

fn lispCaaar(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    return ok(context, caaar(car(args)));
}

fn lispCaadr(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    return ok(context, caadr(car(args)));
}

fn lispCadar(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    return ok(context, cadar(car(args)));
}

fn lispCdaar(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    return ok(context, cdaar(car(args)));
}

fn lispCaddr(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    return ok(context, caddr(car(args)));
}

fn lispCddar(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    return ok(context, cddar(car(args)));
}

fn lispCdadr(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    return ok(context, cdadr(car(args)));
}

fn lispCdddr(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    return ok(context, cdddr(car(args)));
}

fn lispMutReverseInPlace(context: LispRuntimeContext, unusedEnv: Cons, args: Cons): CallResult {
    var list = car(args);
    var prev: Cons = nil;
    while (getType(list) == CT_LIST) {
        val next = list.right;
        list.right = prev;
        prev = list;
        list = next;
    }

    return ok(context, prev);
}
